plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '9.0.0-beta6'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.11.4'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.11.4'
    implementation 'com.google.code.gson:gson:2.11.0'

    def platforms = ["win", "linux", "mac"]
    def javafxDependency = ["javafx-controls", "javafx-fxml"]

    for (plt in platforms) {
        for (dep in javafxDependency) {
            runtimeOnly "org.openjfx:$dep:$javafx.version:$plt"
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events = ["passed", "skipped", "failed"]

        showExceptions = true
        exceptionFormat = "full"
        showCauses = true
        showStackTraces = true
        showStandardStreams = false
    }
}

application {
    mainClass.set("chatbot.Launcher")
}

javafx {
    version = '21.0.6'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

shadowJar {
    archiveBaseName = "shade"
    archiveClassifier = null
}

checkstyle {
    toolVersion = '10.21.2'
}

run{
    standardInput = System.in
    enableAssertions = true
}
